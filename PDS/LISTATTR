LAT      TITLE 'LISTATTR - TSO COMMAND TO LIST ATTRIBUTE LIST ATTRIBUTE+
               S.'
*%A RENT AMODGEN PPLINK ADATA NOTEST
*L ALIAS LAT
* ------------------------------------------------------------------- *
         MACRO
&LABEL   APUT  &MSG,                    @MESSAGE TEXT                  &
               &LEN,                    @LENGTH OF MESSAGE             &
               &RSA=,                   OPTIONAL 2 WORD SAVE AREA      &
               &EPA=                    EPUTL EPA
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.*  --> E@PUT
.*
.*  THIS MACRO SETS UP A PARAMETERS AND INVOKES THE EPUTL ROUTINE.
.*
.*
.*  ARGUMENTS: -----------------------------------------------------
.*
.*      &MSG : ADDRESS OF MESSAGE TEXT OR A LITERAL
.*
.*      &LEN : LENGTH OF MESSAGE TEXT (DEFAULT IS L'&MSG)
.*
.*      &RSA :(OPTIONAL) 1 WORD SAVE AREA TO PRESERVE R14 FOR TPUT
.*            COMPATIBILITY.
.*
.*      &EPA :(OPTIONAL) EPA FOR EPUTL ROUTINE
.*
.*
.*   EXAMPLES: -----------------------------------------------------
.*
.*      1) APUT  'LITERAL'
.*
.*      2) APUT  (R3),80
.*
.*      3) APUT  LABEL
.*
.*      4) APUT  LABEL,40
.*
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.*
.*  LOCAL SET SYMBOLS
.*
         LCLC  &SMA                     MESSAGE ADDRESS (REG)
         LCLC  &SML                     MESSAGE LENGTH (REG)
         LCLC  &SEP                     EPA (REGISTER)
.*
         LCLC  &L1,&L2,&L3              LOCAL LABELS
.*
&L1      SETC  'EP'.'&SYSNDX'.'L1'      LOCAL LABELS
&L2      SETC  'EP'.'&SYSNDX'.'L2'      LOCAL LABELS
&L3      SETC  'EP'.'&SYSNDX'.'L3'      LOCAL LABELS
.*
.*
.*  MESSAGE ADDRESS
.*
         AIF   (T'&LABEL EQ 'O').A0500                      *HMD 11/86*
.*                                                          *HMD 11/86*
&LABEL   DS    0H                                           *HMD 11/86*
.*                                                          *HMD 11/86*
.A0500   ANOP                                               *HMD 11/86*
         AIF   ('&MSG' NE '').A2000
         MNOTE 8,'''&MSG='' MISSING / REQUIRED'
         MEXIT
.*
.A2000   ANOP
         AIF   ('&MSG'(1,1) NE '(').A2400
&SMA     SETC  '&MSG'(2,K'&MSG-2)
         LR    R1,&SMA                  @MESSAGE TEXT
         AGO   .A2600
.*
.A2400   ANOP
         AIF   ('&MSG'(1,1) NE '''').A2500
         B     &L3                      BRANCH AROUND MESSAGE
&L2      DC    C&MSG
&L3      DS    0H
         LA    R1,&L2                   @MESSAGE
         AGO   .A2600
.*
.A2500   ANOP
         LA    R1,&MSG                  @MESSAGE TEXT
.A2600   ANOP
.*
.*  MESSAGE LENGTH
.*
         AIF   ('&LEN' NE '').A3000
         AIF   ('&MSG'(1,1) NE '''').A2800
         LA    R0,L'&L2                 MESSAGE LENGTH
         AGO   .A3600
.*
.A2800   ANOP
         AIF   ('&MSG'(1,1) EQ '(').A2900
         LA    R0,L'&MSG                MESSAGE LENGTH
         AGO   .A3600
.*
.A2900   ANOP
         MNOTE 8,'''LEN'' MISSING / REQUIRED'
         MEXIT
.*
.A3000   ANOP
         AIF   ('&LEN'(1,1) NE '(').A3400
&SML     SETC  '&LEN'(2,K'&LEN-2)
         LR    R0,&SML                  MESSAGE LENGTH
         AGO   .A3600
.*
.A3400   ANOP
         LA    R0,&LEN                  MESSAGE LENGTH
.A3600   ANOP
.*
.*  RSA
.*
         AIF   ('&RSA' EQ '').A4900
         ST    R14,&RSA                 SAVE REGS 14
.A4900   ANOP
.*
.*  ENTRY POINT
.*
         AIF   ('&EPA' NE '').A5000
         L     R15,=V(EPUTL)            EPA
         AGO   .A5600
.*
.A5000   AIF   ('&EPA'(1,1) NE '(').A5400
&SEP     SETC  '&EPA'(2,K'&EPA-2)
         LR    R15,&SEP                 EPA
         AGO   .A5600
.*
.A5400   ANOP
         L     R15,&EPA                 EPA
.*
.A5600   ANOP
.*
.*  CALL LINKAGE
.*
.ACALL   BALR  R14,R15                  CALL PUTLINE ROUTINE
.*
         AIF   ('&RSA' EQ '').A6900
         L     R14,&RSA                 RESTORE R14
.A6900   ANOP
.*
.AMEND   MEND
* ------------------------------------------------------------------- *
         SPACE 3
LISTATTR CSECT
***********************************************************************
*                                                                     *
*  THIS PROGRAM IS IN THE PUBLIC DOMAIN.                              *
*  ITS SOURCE IS THE SHARE/GUIDE MVS TAPE, AND HAS BEEN MODIFIED BY:  *
*                                                                     *
*                                  JEFFREY R. BROIDO                  *
*                                  broidoj@gmail.com                  *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        LISTATTR  -  DAVID G. SAGER  -  FEB, 1976                    *
*                                                                     *
*        ATTRIBUTES - REENTRANT, REFRESHABLE, REUSEABLE               *
*                                                                     *
*        SYNTAX  -    LISTATTR  | A                                   *
*                     LAT                                             *
*                                                                     *
*                                                                  JRB*
*        MODIFIED BY J. BROIDO TO TAKE A PARM OF 'A' TO TELL       JRB*
*        PROGRAM TO LIST ATTRIBUTES OF ALL ALLOCATED FILES.        JRB*
*                                                                     *
*        Added PUTLINE output, fixed LA and ST and L instructions     *
*        not to use the index register.  (SBG)                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                   JRB
         YREGS                                                     JRB
         EJECT
         SAVE  (14,12),,*
         LR    R12,R15
         USING LISTATTR,R12
         LR    R2,R1              A(CPPL)
         GETMAIN R,LV=WKAL
         LR    R11,R1
         USING WKA,R11
         LA    R10,SAVE
         ST    R13,SAVE+4
         ST    R10,8(,R13)
         LR    R13,R10
         MVI   SW,X'00'
         LTR   R2,R2               CK. PARM POINTER.               JRB
         BZ    FCONT1              EMPTY. FORGET IT.               JRB
         L     R3,0(,R2)           POINT TO CMD BUFF. OR OS PARM.  JRB
         CH    R3,=H'00'           CHECK LENGTH.                   JRB
         BE    FCONT1              EMPTY PARM. FORGET IT.          JRB
         CLI   2(R3),X'00'         OS PARM?                        JRB
         BE    CPPLPARM            B IF NOT.                       JRB
         LA    3,2(,R3)            POINT TO OS PARM.               JRB
         B     FCONT0              CONTINUE.                       JRB
CPPLPARM LH    R4,2(,R3)           LOAD BUFFER OFFSET.             JRB
         CH    R4,0(,R3)           OVER THE TOP?                   JRB
         BNL   FCONT1              B IF YES.                       JRB
         AR    R3,R4               POINT ...                       JRB
         LA    R3,4(,R3)           ... TO PARM.                    JRB
FCONT0   OI    0(R3),C' '          UPPERCASE.                      JRB
         CLI   0(R3),C'A'          LIST ATTR. OF ALL DATASETS?     JRB
         BNE   FCONT1              B IF NOT.                       JRB
         OI    SW,ALLFLG           TURN ON LISTALL FLAG.           JRB
FCONT1   L     R3,16                                  CVT
         L     R3,0(,R3)                              NEW/CURRENT TCB
         L     R3,4(,R3)                              CURRENT TCB
         L     R3,12(,R3)                             TIOT
         LA    R3,24(,R3)         FIRST TIOT ENTRY
         USING TIOELNGH,R3
TIOTENT  XR    R4,R4
         IC    R4,TIOELNGH        LENGTH OF THIS ENTRY
         LTR   R4,R4              END OF TIOT?
         BZ    EXIT               YES, EXIT
         CLI   TIOEDDNM,X'00'     VALID DDNAME?
         BE    NEXTTIO            NO, CONTINUE
         CLI   TIOEDDNM,X'40'     VALID DDNAME?
         BE    NEXTTIO            NO, CONTINUE
         XR    R5,R5
         ICM   R5,7,TIOEJFCB      A(JFCB)-16
         BZ    NEXTTIO            IF ZERO, SKIP
         LA    R5,16(,R5)          A(JFCB)
         TM    SW,ALLFLG           LIST ATTRIBUTES OF ALL FILES?   JRB
         BO    FORMATA             B IF YES.                       JRB
         USING INFMJFCB,R5
         CLC   JFCBDSNM(44),NULLFILE  DSNAME = NULLFILE?
         BE    FORMAT             YES, REPRESENTS ATTRIBUTE
NEXTTIO  DS    0H
         AR    R3,R4              POINT TO NEXT TIOT ENTRY
         B     TIOTENT
FORMAT   DS    0H
         TM    TIOELINK,TIOTTERM  IS THIS A TERMINAL FILE?
         BO    NEXTTIO            YES, SKIP
FORMATA  OI    SW,ANYATTR          TURN ON ATTR LIST FOUND FLG.    JRB
         MVI   OUT,X'40'                              BLANK OUT
         MVC   OUT+1(249),OUT
         LA    R10,OUT
         MVC   0(8,R10),TIOEDDNM                      MOVE IN NAME
         LA    R10,9(,R10)
TTINPUT  TM    JFCBFLG2,X'80'                           INPUT?
         BNO   TTOUTPUT                                 NO
         MVC   0(5,R10),=CL5'INPUT'
         LA    R10,6(,R10)
         B     TEXPDT
TTOUTPUT TM    JFCBFLG2,X'40'                            OUTPUT?
         BNO   TDIAGNS                                   NO
         MVC   0(6,R10),=CL6'OUTPUT'
         LA    R10,7(,R10)
TDIAGNS  TM    JFCBFLG2,X'04'                            GTF TRACE?
         BNO   TEXPDT                                    NO
         MVC   0(13,R10),=CL13'DIAGNS(TRACE)'
         LA    R10,14(,R10)
TEXPDT   CLC   JFCBXPDT(3),ZERO                       ANY EXPDT?
         BE    TBUFNO                                 NO
         SR    R6,R6                                      PICK
         MVC   0(12,R10),=CL12'EXPDT(00000)'              UP
         IC    R6,JFCBXPDT                                  YEAR
         CVD   R6,DBLEWORD
         UNPK  ANSWER(8),DBLEWORD+4(4)
         OI    ANSWER+7,X'F0'
         MVC   6(2,R10),ANSWER+6
         SR    R6,R6                                      PICK
         LH    R6,JFCBXPDT+1                               UP
         CVD   R6,DBLEWORD                                  DAY
         UNPK  ANSWER(8),DBLEWORD+4(4)                       OF
         OI    ANSWER+7,X'F0'                                 YEAR
         MVC   8(3,R10),ANSWER+5
         LA    R10,13(,R10)
TBUFNO   CLI   JFCBUFNO,X'00'                         ANY BUFNO?
         BE    TBFTEK                                 NO
         SR    R6,R6                                      PICK
         IC    R6,JFCBUFNO                                 UP
         CVD   R6,DBLEWORD                                  BUFNO
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(10,R10),=CL10'BUFNO(000)'
         OI    ANSWER+7,X'F0'
         MVC   6(3,R10),ANSWER+5
         LA    R10,11(,R10)
TBFTEK   CLI   JFCBFTEK,X'00'                         ANY BFTEK?
         BE    TBUFL                                  NO
         MVC   0(8,R10),=CL8'BFTEK(D)'
         TM    JFCBFTEK,X'60'                             AUTO?
         BNO   TTBFTS                                     NO
         MVI   6(R10),C'A'
         B     TBFTEKE
TTBFTS   TM    JFCBFTEK,X'40'                             SIMPLE?
         BNO   TTBFTR                                     NO
         MVI   6(R10),C'S'
         B     TBFTEKE
TTBFTR   TM    JFCBFTEK,X'20'                             RECORD?
         BNO   TTBFTE                                     NO
         MVI   6(R10),C'R'
         B     TBFTEKE
TTBFTE   TM    JFCBFTEK,X'10'                             EXCHANGE
         BNO   TBFALND                                    NO
         MVI   6(R10),C'E'
TBFTEKE  LA    R10,9(,R10)
TBFALND  TM    JFCBFTEK,X'02'                         ANY BFALN(DBLWRD)
         BNO   TBFALNF                                NO
         MVC   0(8,R10),=CL8'BFALN(D)'
         LA    R10,9(,R10)
         B     TBUFL
TBFALNF  TM    JFCBFTEK,X'01'                         ANY BFALN(FLLWRD)
         BNO   TBUFL                                  NO
         MVC   0(8,R10),=CL8'BFALN(F)'
         LA    R10,9(,R10)
TBUFL    CLC   JFCBUFL(2),ZERO                        ANY BUFL
         BE    TRKP                                   NO
         SR    R6,R6                                      PICK
         LH    R6,JFCBUFL                                  UP
         CVD   R6,DBLEWORD                                  BUFL
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(11,R10),=CL11'BUFL(00000)'
         OI    ANSWER+7,X'F0'
         MVC   5(5,R10),ANSWER+3
         LA    R10,12(,R10)
TRKP     CLC   JFCRKP(2),ZERO                         ANY BUFL
         BE    TEROPT                                 NO
         SR    R6,R6                                      PICK
         LH    R6,JFCRKP                                   UP
         CVD   R6,DBLEWORD                                  BUFL
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(10,R10),=CL10'RKP(00000)'
         OI    ANSWER+7,X'F0'
         MVC   4(5,R10),ANSWER+3
         LA    R10,11(,R10)
TEROPT   CLI   JFCEROPT,X'00'                         ANY EROPT?
         BE    TBUFOF                                 NO
         MVC   0(10,R10),=CL10'EROPT(ABE)'
TTACCEPT TM    JFCEROPT,X'80'                             ACCEPT?
         BNO   TTSKIP                                     NO
         MVC   6(3,R10),=CL3'ACC'
         B     TEROPTE
TTSKIP   TM    JFCEROPT,X'40'                             SKIP?
         BNO   TEROPTE                                    NO
         MVC   6(3,R10),=CL3'SKP'
TEROPTE  LA    R10,11(,R10)
TBUFOF   CLI   JFCBUFOF,X'00'                         ANY BUFOF?
         BE    TRECFM                                 NO
         MVC   0(6,R10),=CL6'BUFOF('
         LA    R10,6(,R10)
         TM    JFCBUFOF,X'80'                         L?
         BNO   TBUFOF1                                NO
         MVI   0(R10),C'L'
         LA    R10,1(,R10)
         B     TBUFOFE
TBUFOF1  SR    R6,R6                                      PICK
         IC    R6,JFCBUFOF                                 UP
         CVD   R6,DBLEWORD                                  BUFOF
         UNPK  ANSWER(8),DBLEWORD+4(4)
         OI    ANSWER+7,X'F0'
         MVC   0(3,R10),ANSWER+5
         LA    R10,3(,R10)
TBUFOFE  MVI   0(R10),C')'
         LA    R10,2(,R10)
TRECFM   CLI   JFCRECFM,X'00'                         ANY RECFM?
         BE    TOPTCD                                 NO
         MVC   0(6,R10),=CL6'RECFM('
         LA    R10,6(,R10)
TTUNDEF  TM    JFCRECFM,X'C0'                             UNDEFINED?
         BNO   TTFIXED                                    NO
         MVI   0(R10),C'U'
         LA    R10,1(,R10)
         B     TRECFME
TTFIXED  TM    JFCRECFM,X'80'                             FIXED?
         BNO   TTVARIBL                                   NO
         MVI   0(R10),C'F'
         LA    R10,1(,R10)
TTVARIBL TM    JFCRECFM,X'40'                             VARIBLE
         BNO   TTTRKOV                                    NO
         MVI   0(R10),C'V'
         LA    R10,1(,R10)
TTTRKOV  TM    JFCRECFM,X'20'                             TRKOVERFLW?
         BNO   TTBLKED                                    NO
         MVI   0(R10),C'T'
         LA    R10,1(,R10)
TTBLKED  TM    JFCRECFM,X'10'                             BLOCKED?
         BNO   TTSTDBL                                    NO
         MVI   0(R10),C'B'
         LA    R10,1(,R10)
TTSTDBL  TM    JFCRECFM,X'08'                             STANDARD?
         BNO   TTASAPC                                    NO
         MVI   0(R10),C'S'
         LA    R10,1(,R10)
TTASAPC  TM    JFCRECFM,X'04'                             ASCI CAR CNT?
         BNO   TTMACHCC                                   NO
         MVI   0(R10),C'A'
         LA    R10,1(,R10)
TTMACHCC TM    JFCRECFM,X'02'                             MCHN CAR CNT?
         BNO   TRECFME                                     NO
         MVI   0(R10),C'M'
         LA    R10,1(,R10)
TRECFME  MVI   0(R10),C')'
         LA    R10,2(,R10)
TOPTCD   CLI   JFCOPTCD,X'00'                         ANY OPTCD?
         BE    TDSORG                                 NO
         MVC   0(6,R10),=CL6'OPTCD('
         LA    R10,6(,R10)
         TM    JFCDSORG,X'20'                             DIRECT ACCES?
         BO    TOPTDA                                     YES
TOPTPS   DS    0H
TTWTVCH  TM    JFCOPTCD,X'80'                             VALD CHK?
         BNO   TTBEOF                                     NO
         MVI   0(R10),C'W'
         LA    R10,1(,R10)
TTBEOF   TM    JFCOPTCD,X'40'                             BYPASS EOF?
         BNO   TTCHNSCH                                   NO
         MVI   0(R10),C'B'
         LA    R10,1(,R10)
TTCHNSCH TM    JFCOPTCD,X'20'                             CHAIN SCHD?
         BNO   TTANSITR                                   NO
         MVI   0(R10),C'C'
         LA    R10,1(,R10)
TTANSITR TM    JFCOPTCD,X'08'                             TRANSLATE?
         BNO   TTREDERR                                   NO
         MVI   0(R10),C'Q'
         LA    R10,1(,R10)
TTREDERR TM    JFCOPTCD,X'04'                             REDUCED ERR?
         BNO   TTUSERT                                    NO
         MVI   0(R10),C'Z'
         LA    R10,1(,R10)
TTUSERT  TM    JFCOPTCD,X'02'                             USER TOTAL?
         BNO   TOPTCDJ                                    NO
         MVI   0(R10),C'T'
         LA    R10,1(,R10)
TOPTCDJ  TM    JFCOPTCD,X'01'                             3800 CHAR?
         BNO   TOPTCDE                                    NO
         MVI   0(R10),C'J'
         LA    R10,1(,R10)
         B     TOPTCDE
TOPTDA   DS    0H
TTWTVCH1 TM    JFCOPTCD,X'80'                             VALD CHK?
         BNO   TTOVFL                                     NO
         MVI   0(R10),C'W'
         LA    R10,1(,R10)
TTOVFL   TM    JFCOPTCD,X'40'                             TRK OVFLOW?
         BNO   TTEXTS                                     NO
         MVI   0(R10),C'T'
         LA    R10,1(,R10)
TTEXTS   TM    JFCOPTCD,X'20'                             EXT SEARCH?
         BNO   TTFDBK                                     NO
         MVI   0(R10),C'E'
         LA    R10,1(,R10)
TTFDBK   TM    JFCOPTCD,X'10'                             FEEDBACK?
         BNO   TTACTADR                                   NO
         MVI   0(R10),C'F'
         LA    R10,1(,R10)
TTACTADR TM    JFCOPTCD,X'08'                             ACTUAL ADDR?
         BNO   TTRELBLK                                   NO
         MVI   0(R10),C'A'
         LA    R10,1(,R10)
TTRELBLK TM    JFCOPTCD,X'01'                             RELATIVE BLK?
         BNO   TOPTCDE                                    NO
         MVI   0(R10),C'R'
         LA    R10,1(,R10)
         B     TOPTCDE
TOPTCDE  MVI   0(R10),C')'
         LA    R10,2(,R10)
TDSORG   CLI   JFCDSORG,X'00'                         ANY DSORG?
         BE    TBLKSI                                 NO
         MVC   0(10,R10),=CL10'DSORG(** )'
TTIS     TM    JFCDSORG,X'80'                             IS?
         BNO   TTPS                                       NO
         MVC   6(2,R10),=CL2'IS'
TTPS     TM    JFCDSORG,X'40'                             PS?
         BNO   TTDA                                       NO
         MVC   6(2,R10),=CL2'PS'
TTDA     TM    JFCDSORG,X'20'                             DA?
         BNO   TTPO                                       NO
         MVC   6(2,R10),=CL2'DA'
TTPO     TM    JFCDSORG,X'02'                             PO?
         BNO   TTU                                        NO
         MVC   6(2,R10),=CL2'PO'
TTU      TM    JFCDSORG,X'01'                             UNMOVABLE?
         BNO   TDSORGE                                    NO
         MVI   8(R10),C'U'
TDSORGE  LA    R10,11(,R10)
TLIMCT   TM    JFCDSORG,X'20'                         DA?
         BNO   TBLKSI                                 NO, NO LIMCT
         CLC   JFCLIMCT(3),ZERO                       ANY LIMCT?
         BE    TBLKSI                                 NO
         SR    R6,R6                                      PICK
         ICM   R6,7,JFCLIMCT                               UP
         CVD   R6,DBLEWORD                                  LIMCT
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(9,R10),=CL9'LIMCT(00)'
         OI    ANSWER+7,X'F0'
         MVC   6(2,R10),ANSWER+6
         LA    R10,10(,R10)
TBLKSI   CLC   JFCBLKSI(2),ZERO                       ANY BLKSIZE?
         BE    TLRECL                                 NO
         SR    R6,R6                                      PICK
         LH    R6,JFCBLKSI                                 UP
         CVD   R6,DBLEWORD                                  BLKSIZE
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(14,R10),=CL14'BLKSIZE(00000)'
         OI    ANSWER+7,X'F0'
         MVC   8(5,R10),ANSWER+3
         LA    R10,15(,R10)
TLRECL   CLC   JFCLRECL(2),ZERO                       ANY LRECL?
         BE    TNCP                                   NO
         CLC   JFCLRECL(2),=XL2'8000'  LRECL=X?
         BNE   TLRECL1            NO
         MVC   0(8,R10),=CL8'LRECL(X)'
         LA    R10,9(,R10)
         B     TNCP
TLRECL1  SR    R6,R6                                      PICK
         LH    R6,JFCLRECL                                 UP
         CVD   R6,DBLEWORD                                  LRECL
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(12,R10),=CL12'LRECL(00000)'
         OI    ANSWER+7,X'F0'
         MVC   6(5,R10),ANSWER+3
         LA    R10,13(,R10)
TNCP     CLI   JFCNCP,X'00'                           ANY NCP?
         BE    TDEN                                   NO
         SR    R6,R6                                      PICK
         IC    R6,JFCNCP                                   UP
         CVD   R6,DBLEWORD                                  NCP
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(7,R10),=CL7'NCP(00)'
         OI    ANSWER+7,X'F0'
         MVC   4(2,R10),ANSWER+6
         LA    R10,8(,R10)
TDEN     CLI   JFCDEN,X'00'                           ANY DEN?
         BE    TTRTCH                                 NO
         MVC   0(6,R10),=CL6'DEN(*)'
TTDEN0   CLI   JFCDEN,X'03'                               200 BPI?
         BNE   TTDEN1                                     NO
         MVI   4(R10),C'0'
TTDEN1   CLI   JFCDEN,X'43'                               556 BPI?
         BNE   TTDEN2                                     NO
         MVI   4(R10),C'1'
TTDEN2   CLI   JFCDEN,X'83'                               800 BPI?
         BNE   TTDEN3                                     NO
         MVI   4(R10),C'2'
TTDEN3   CLI   JFCDEN,X'C3'                               1600 BPI?
         BNE   TTDEN4                                     NO
         MVI   4(R10),C'3'
TTDEN4   CLI   JFCDEN,X'D3'                               6250 BPI?
         BNE   TDENE                                      NO
         MVI   4(R10),C'4'
TDENE    LA    R10,7(,R10)
TTRTCH   CLI   JFCTRTCH,X'00'                         ANY DEN?
         BE    WRITE                                  NO
         MVC   0(9,R10),=CL9'TRTCH(* )'
TTTRE    CLI   JFCTRTCH,X'23'                             E?
         BNE   TTTRT                                      NO
         MVI   6(R10),C'E'
         B     TTRTCHE
TTTRT    CLI   JFCTRTCH,X'3B'                             T?
         BNE   TTTRC                                      NO
         MVI   6(R10),C'T'
         B     TTRTCHE
TTTRC    CLI   JFCTRTCH,X'13'                             C?
         BNE   TTTRET                                     NO
         MVI   6(R10),C'C'
         B     TTRTCHE
TTTRET   CLI   JFCTRTCH,X'2B'                             ET?
         BNE   TKEYLEN                                    NO
         MVC   6(2,R10),=CL2'ET'
         B     TTRTCHE
TKEYLEN  SR    R6,R6                                      PICK
         IC    R6,JFCKEYLE                                 UP
         CVD   R6,DBLEWORD                                  KEYLEN
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(11,R10),=CL11'KEYLEN(000)'
         OI    ANSWER+7,X'F0'
         MVC   7(3,R10),ANSWER+5
TTRTCHE  LA    R10,12(,R10)
         SPACE  2
WRITE    LA    R6,OUT
         SR    R10,R6
*
*              PRINT ONLY 79 CHARACTERS PER LINES
*              MAKE SURE LAST CHARACTER OF A LINE IS A BLANK
*
GTRTHN79 LA    R7,79
         CR    R10,R7
         BNH   TPUTFNL
         CLI   79(R6),C' '
         BNE   ADD
         LA    R7,1(R7)
         B     TPUT
ADD      AR    R7,R6
DECRMNT  S     R7,=F'1'
         CLI   0(R7),C' '
         BNE   DECRMNT
         SR    R7,R6
TPUT     APUT  (R6),(R7)
         AR    R6,R7
         SR    R10,R7
         B     GTRTHN79
TPUTFNL  APUT  (R6),(R10)
         B     NEXTTIO
EXIT     DS    0H
         TM    SW,ANYATTR         ANY ATTR?                        JRB
         BO    RETURN             YES
         APUT  =C'NO ATTRIBUTE LISTS WERE FOUND.',30
*        VTPUT 'NO ATTRIBUTE LISTS WERE FOUND.'
RETURN   L     R13,SAVE+4
         LR    R1,R11
         FREEMAIN R,LV=WKAL,A=(1)
         L     R13,SAVE+4
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         EJECT
ZERO     DC    F'0'
BLANK    DC    CL4' '
NULLFILE DC    CL44'NULLFILE'
WKA      DSECT
SAVE     DS    18F
DBLEWORD DS    D
ANSWER   DS    D
OUT      DS    CL250
         SPACE 1                                                   JRB
SW       DS    X                   FLAGS.                          JRB
ANYATTR  EQU   B'10000000'         ATTRIBUTE LIST FOUND FLAG.      JRB
ALLFLG   EQU   B'01000000'         ALL LIST FLAG.                  JRB
         SPACE 1                                                   JRB
WKAE     EQU   *
WKAL     EQU   WKAE-WKA
         IEFTIOT1
         IEFJFCBN
         END
EPUTL    TITLE 'PUTLINE FOR FEWER THAN 83 ARGUMENTS IN THE KEY OF C MAJ&
               OR'
EPUTL    CSECT                                              *HMD 10/85*
         LCLB  &XA                                          *HMD 10/85*
&XA      SETB  1                  SET TO ZERO ON NON/XA     *HMD 10/85*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  MODULE NAME  . . . . EPUTL
*
*  ALIASES  . . . . . . NONE
*
*  DATE WRITTEN . . . . 06/85
*  UPDATED      . . . . 04/88                                  HD APR88
*  CLEANED A BIT  . . . 08/17
*
*  AUTHOR . . . . . . . JIM ELSWORTH
*  UPDATER  . . . . . . HOWARD M. DEAN                         HD APR88
*
*  ENVIRONMENT  . . . . TSO COMMAND PROCESSOR
*
*  PURPOSE:
*    ALLOW AN EASY INTERFACE TO PUTLINE ROUTINE
*
*
*  DESCRIPTION:
*    BUILDS PUTLINE PARAMETER LIST BASED ON INPUT ARGUMENTS.
*    ISSUES SINGLE LINE PUTLINE.
*
*
*  ATTRIBUTES:
*    REENTRANT
*
*  FEATURES:
*    WILL RUN UNDER BATCH TMP SINCE PUTLINE USED INSTEAD OF TPUT.
*
*  CALLED MODULES:
*    PUTLINE
*
*  ASSUMPTIONS:
*    CALLED BY A TSO-CP THAT IS OBSERVING STANDARD LINKAGE
*
*  ABENDS:
*    POSSIBLE 0CX IF BAD ARGUMENTS
*
*  CAVEATS:
*    UNPREDICTABLE RESULTS IF FIRST PROGRAM RUNNING UNDER THIS TCB
*    DID NOT SAVE (14,12) BEFORE R1 WAS MODIFIED.
*
*
*  INPUT ARGUMENTS:
*    1)  R1 : ADDRESS OF MESSAGE TEXT
*    2)  R0 : LENGTH OF MESSAGE TEXT
*    3) R13 : STANDARD OS SAVE AREA
*    4) R14 : RETURN ADDRESS
*    5) R15 : ENTRY ADDRESS
*
*               ( CONTINUED ... )
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
*  OUTPUT ARGUMENT: RETURN CODE FROM PUTLINE OR
*
RCBA     EQU   100                 BAD MESSAGE ADDRESS
RCBL     EQU   104                 MESSAGE LENGTH <1 OR >MAXMSG
*
MAXMSG   EQU   255                 MAXIMUM MESSAGE LENGTH
*
*
*  REGISTERS USED:
*
R0       EQU   0                   MESSAGE LENGTH AT ENTRY
R1       EQU   1                   MESSAGE ADDRESS AT ENTRY
R2       EQU   2                   WORK
R3       EQU   3                   WORK
RMA      EQU   4                   @MESSAGE TEXT
RML      EQU   5                   MESSAGE LENGTH
RUPT     EQU   6                   @UPT (USER PROFILE TABLE)
RECT     EQU   7                   @ECT
R12      EQU   12                  CSECT BASE
R13      EQU   13                  @SAVE AREA AND DYNAMIC WORK AREA
R14      EQU   14                  WORK/LINKAGE
R15      EQU   15                  WORK/LINKAGE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         IKJCPPL
         EJECT
         IKJTCB   LIST=YES                                  *HMD 10/85*
         EJECT                                              *HMD 10/85*
         CVT      DSECT=YES,LIST=YES                        *HMD 10/85*
         EJECT
         IHAPSA   LIST=YES                                     HD APR88
         EJECT                                                 HD APR88
PUTLDS   DSECT
         DS    18F                      OS SAVE AREA
PUTAAA   EQU   *
PUTECB   DS    F                        ECB
PUTIOPL  DS    4F                       IOPLADS
PUTBLOK  PUTLINE MF=L
PUTLEN   DS    H                        LENGTH
PUTZZZ   DS    H                        RESERVED
PUTMSG   DS    (MAXMSG)C                MESSAGE TEXT AREA
         DS    0D
PUTSIZE  EQU   *-PUTLDS                 SIZE OF PUTLINE PARMS
         EJECT
***********************************************************************
*  SET UP LINKAGE
***********************************************************************
         SPACE 1
EPUTL    CSECT
         AIF  (NOT &XA).X001                                *HMD 10/85*
EPUTL    AMODE 31
.X001    ANOP                                               *HMD 10/85*
         STM   R14,R12,12(R13)    SAVE CALLERS REGS
         LR    R12,R15            COPY EPA
         USING EPUTL,R12          ADDRESSABILITY
         AIF   (NOT &XA).X002                               *HMD 10/85*
         GETMAIN RU,              LV=LENGTH                            &
               LV=PUTSIZE,        STANDARD OS SA PLUS ANY EXTRA        &
               LOC=RES            RELATIVE TO 16MB LINE
         AGO   .X003                                        *HMD 10/85*
.X002    ANOP                                               *HMD 10/85*
         GETMAIN RU,              LV=LENGTH                            &
               LV=PUTSIZE         STANDARD OS SA PLUS ANY EXTRA
.X003    ANOP                                               *HMD 10/85*
         ST    R1,8(,R13)         OUR SAVE AREA POINTER
         ST    R13,4(,R1)         POINTER TO CALLER'S SAVE AREA
         LR    R13,R1             @OUR SAVE AREA
         USING PUTLDS,R13         MAP SA AND WORK AREA
         L     R14,4(,R13)        POINT TO CALLER'S RSA
         LM    R0,R1,20(R14)      RESTORE REGS CHANGED BY GETMAIN
         SPACE 2
***********************************************************************
*  UNLOAD ARGUMENTS AND VALIDATE WHERE POSSIBLE
***********************************************************************
         SPACE 1
         LA    R15,RCBA                 RC IF BAD ADDRESS
         LTR   RMA,R1                   MESSAGE ADDRESS
         BNP   RETURN                   GOOD ASSUMPTION
         LA    R15,RCBL                 RC IF BAD LENGTH
         LTR   RML,R0                   POSITIVE?
         BNP   RETURN                   -NO
         C     RML,=A(MAXMSG)           MORE THAN MAX?
         BH    RETURN                   -YES
         SPACE 2
***********************************************************************
*  BUILD PARAMETER LIST
***********************************************************************
         SPACE 1
         LA    R15,4(,RML)              MESSAGE LENGTH
         STH   R15,PUTLEN               PLUS HEADER
         XC    PUTZZZ,PUTZZZ            CLEAR 2ND HWORD OF RDW
         BCTR  RML,0                    REDUCE FOR MOVE
         EX    RML,PUTMOVE
         EJECT
***********************************************************************
*  DIG UP ADDRESS OF UPT AND ECT
***********************************************************************
         SPACE 1
         L     R2,PSATOLD-PSA           @TCB                   HD APR88
         L     R2,TCBFSA-TCB(,R2)       @1ST PROBLEM PGM SAVE AREA
         L     R2,24(,R2)               REG1 THAT WAS PASSED TO CP
         USING CPPL,R2                  MAP CPPL
         L     RUPT,CPPLUPT             @UPT
         L     RECT,CPPLECT             @ECT
         SPACE 2
***********************************************************************
*  ISSUE PUTLINE CALL
***********************************************************************
         SPACE 1
         XC    PUTECB,PUTECB            CLEAR ECB
         XC    PUTBLOK(12),PUTBLOK      CLEAR PARM BLOCK       HD APR88
         PUTLINE PARM=PUTBLOK,          PARAMETER BLOCK                &
               UPT=(RUPT),              @UPT                           &
               ECT=(RECT),              @ECT                           &
               ECB=PUTECB,              @ECB                           &
               OUTPUT=(PUTLEN,TERM,SINGLE,DATA),                       &
               MF=(E,PUTIOPL)
         EJECT
***********************************************************************
*  RETURN TO CALLER
***********************************************************************
         SPACE 1
RETURN   EQU   *
         LR    R2,R13                   @OUR SAVE AREA
         LR    R3,R15                   SAVE RETURN CODE
         L     R13,4(,R13)              @CALLER'S SAVE AREA
         FREEMAIN RU,LV=PUTSIZE,A=(R2),SP=0
         LR    R15,R3                   RESTORE RC
         RETURN (14,12),RC=(15)         RESTORE CALLER'S REGS
         SPACE 5
***********************************************************************
*  CONSTANTS AND NON-REENTRANT WORK AREAS
***********************************************************************
         SPACE 1
PUTMOVE  MVC   PUTMSG(*-*),0(RMA)       MOVE MESSAGE TO BUFFER
         SPACE 2
         LTORG
         SPACE 2
         END   EPUTL
